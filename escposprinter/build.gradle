plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libraryVersionCode = 3003000
def libraryVersionName = "3.3.0"

android {
    namespace 'com.dantsu.escposprinter'
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        // Publishes "fullRelease" build variant with "fullRelease" component created by
        // Android Gradle plugin
        singleVariant("release")
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.hierynomus:smbj:0.13.0'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:2.0.21"))
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.zxing:core:3.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.DantSu'
                artifactId = 'ESCPOS-ThermalPrinter-Android'
                version = libraryVersionName
                pom {
                    name = 'Android library for ESC/POS Thermal Printer'
                    description = 'Useful library to help Android developers to print with (Bluetooth, TCP, USB) ESC/POS thermal printers.'
                }
                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}